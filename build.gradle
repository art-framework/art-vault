buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
}

plugins {
    id 'java'
    id 'jacoco'
    id 'idea'
    id 'io.freefair.lombok' version '6.2.0'
    id 'kr.entree.spigradle' version '2.2.3'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

apply from: "$rootDir/gradle/jacoco.gradle"
apply from: "$rootDir/gradle/publish.gradle"

sourceCompatibility = 11
targetCompatibility = 11

ext {
    mcVersion = project.property("mcVersion")
}

spigot {
    name = project.property("pluginName")
    authors = [project.property("author")]
    apiVersion = project.property("apiVersion")
    depends = ['art-framework', 'Vault']
}

archivesBaseName = project.property("pluginName")

group project.property("group")

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

repositories {
    mavenCentral()
    jcenter()

    maven { url = 'https://jitpack.io' }
    spigot()
    maven { url = 'https://repo.aikar.co/content/groups/aikar/' }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url = 'https://maven.enginehub.org/repo/' }
}

dependencies {
    implementation spigot(mcVersion)

    implementation 'io.art-framework.art-framework:api:4.4.0'
    implementation 'de.raidcraft.economy:wrapper:2.3.2'

    //Test dependencies
    testImplementation 'org.junit.jupiter:junit-jupiter:5.+'
    testImplementation "org.mockito:mockito-core:3.+"
    testImplementation 'org.assertj:assertj-core:3.+'
}

shadowJar {
    classifier = ''
    dependencies {
        include(dependency('de.raidcraft.economy:wrapper:'))
    }
    relocate 'de.raidcraft.economy.wrapper', "shadow.${pluginName}.economy"
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    ignoreFailures = false
}

processResources {
    project.properties.put("version", this.version)
    expand project.properties
}

defaultTasks 'build'
